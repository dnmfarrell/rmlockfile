#!/usr/bin/env perl
use Getopt::Long 'HelpMessage';
use strict;
use warnings;

GetOptions (
  'process=s'  => \my $process,
  'filepath=s' => \my $filepath,
  'help=s'     => sub { HelpMessage(0) },
) or die HelpMessage(1);

HelpMessage(1) unless $process && $filepath;

my @process_table = `ps -e`;

my @process_names;
for my $row (@process_table)
{
  $row =~ s/^\s+//;
  my ($pid, $tty, $time, $process_name)  = split /\s+/, $row;
  push @process_names, $process_name;
}

exit(0) if grep(/^$process$/, @process_names);

unlink $filepath;

=head1 NAME

rmlockfile - remove an orphaned lockfile when its process has finished

=head1 SYNOPSIS

  rmlockfile [options]

  Options
  -------
  --process,-p    process name to check is running
  --filepath,-f   path of the lockfile to remove if process is not running
  --help,-h       display these options

  Command line examples
  ---------------------
  $ ./rmlockfile --process keepassx --filepath ~/Documents/vault.kdb.lock

  $ ./rmlockfile -p keepassx -f ~/Documents/vault.kdb.lock

  Crontab example
  ---------------
  * * * * * /home/user1/rmlockfile -p keepassx -f /home/user1/Documents/vault.kdb.lock

=head1 DESCRIPTION

C<rmlockfile> is a lightweight program for tidying up orphaned lockfiles after
their process has finished. My primary use-case for this was keepassx, which
will always leave behind a lockfile if interrupted or terminated.

=head1 REQUIREMENTS

Perl v5.8 or higher, GNU Bash (tested on v4.3.42, Fedora 22)

=head1 AUTHOR

David Farrell E<copy> 2015

=head1 LICENSE

FreeBSD, see LICENSE
